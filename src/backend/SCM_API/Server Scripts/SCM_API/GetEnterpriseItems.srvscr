/********************************************************************************
Description.. :	Get enterprise tree
Author....... :	MARIUS, DC
Date......... : 2018-11-06
*******************************************************************************/;
:DECLARE sOutType, aItems, aEnterpriseCategories, i, oResult, sDataSet, oDataSet, bFolder,
		 aApplicationSubCategories, aTablesSubCategories, sURI, oRow, sGUID, sCheckedOutBy,
		 sItemName;

:IF Request:QueryString:IsProperty("URI");
	sURI := Request:QueryString:URI;
:ELSE;
	sURI:= "";
:ENDIF;

:DECLARE oUtils;
oUtils := CreateUDObject("SCM_API.Utils", {});

:DECLARE oEnterpriseItem;
oEnterpriseItem := oUtils:ParseURI(sURI);

aItems := {};

aEnterpriseCategories := {
	{"Applications", "CATEGORY", "N/A", sURI + "/Applications", NIL, NIL},
	{"Server Scripts", "CATEGORY", "N/A", sURI + "/ServerScripts", NIL, NIL},
	{"Client Scripts", "CATEGORY", "N/A", sURI + "/ClientScripts", NIL, NIL},
	{"Data Sources", "CATEGORY", "N/A", sURI + "/DataSources", NIL, NIL},
	{"Tables", "CATEGORY", "N/A", sURI + "/Tables", NIL, NIL},
	{"Server Logs", "CATEGORY", "N/A", sURI + "/ServerLogs", NIL, NIL}
};

aTablesSubCategories := {
	{"Database", "ENT_TABLES_DATABASE", "N/A", sURI + "/Database", NIL, NIL},
	{"Dictionary", "ENT_TABLES_DICTIONARY", "N/A", sURI + "/Dictionary", NIL, NIL}
};

aApplicationSubCategories := {
	{"HTML Forms", "ENT_APP_HTML_FRM", "N/A", sURI + "/HTMLForms", NIL, NIL},
	{"XFD Forms", "ENT_APP_XFD_FRM", "N/A", sURI + "/XFDForms", NIL, NIL},
	{"Server Scripts", "ENT_APP_SS", "N/A", sURI + "/ServerScripts", NIL, NIL},
	{"Client Scripts", "ENT_APP_CS", "N/A", sURI + "/ClientScripts", NIL, NIL},
	{"Data Sources", "ENT_APP_DS", "N/A", sURI + "/DataSources", NIL, NIL}
};

bFolder := .T.;

:BEGINCASE;
	:CASE oEnterpriseItem:Type == "ROOT";
		aItems := aEnterpriseCategories;
		sOutType := "CATEGORY";
	:EXITCASE;
	
	:CASE oEnterpriseItem:Type == "ENT_CAT_APPS";
		sOutType := "APPCATEGORY";
		sDataSet := DoProc("Enterprise_Data_Providers.AppCategProvider.GetList", {});
		aItems := DoProc("_GetCategoryItems", {sDataSet, sOutType, sURI});
	:EXITCASE;
	
	:CASE oEnterpriseItem:Type == "ENT_CAT_SS";
		sOutType := "SSCAT";
		sDataSet := DoProc("Enterprise_Data_Providers.ServerScriptCategProvider.GetList", {});
		aItems := DoProc("_GetCategoryItems", {sDataSet, sOutType, sURI});
	:EXITCASE;
	
	:CASE oEnterpriseItem:Type == "ENT_CAT_CS";
		sOutType := "CSCAT";
		sDataSet := DoProc("Enterprise_Data_Providers.ClientScriptCategProvider.GetList", {});
		aItems := DoProc("_GetCategoryItems", {sDataSet, sOutType, sURI});
	:EXITCASE;
	
	:CASE oEnterpriseItem:Type == "ENT_CAT_DS";
		sOutType := "DSCAT";
		sDataSet := DoProc("Enterprise_Data_Providers.DataSourceCategProvider.GetList", {});
		aItems := DoProc("_GetCategoryItems", {sDataSet, sOutType, sURI});
	:EXITCASE;
	
	:CASE oEnterpriseItem:Type == "APP";
		aItems := aApplicationSubCategories;
		sOutType := "CATEGORY";
	:EXITCASE;
	
	:CASE oEnterpriseItem:Type == "APPCATEGORY";
		sOutType := "APP";
		:DECLARE sCategoryID;
		sCategoryID := LSearch("select CATEGORYID from LIMSAPPCATEGORIES where CATNAME=?", "", "DICTIONARY", {Upper(oEnterpriseItem:Name)});
		sDataSet := DoProc("Enterprise_Data_Providers.AppProvider.GetList", {sCategoryID});
		oDataSet := DoProc("Enterprise_Server.DataSetSupport.DsFromString", {sDataSet});
		:FOR i := 0 :TO oDataSet:Tables[0]:Rows:Count - 1;
			oRow := oDataSet:Tables[0]:Rows[i];
			AAdd(aItems, {oRow["APPNAME"], sOutType, "N/A", sURI + "/" + oRow["APPNAME"], NIL, NIL});
		:NEXT;
	:EXITCASE;

	:CASE oEnterpriseItem:Type == "APPSUBCATEGORY" .AND. oEnterpriseItem:Name == "ClientScripts";
		sOutType := "APPCS";
		bFolder := .F.;
		sDataSet := DoProc("Enterprise_Data_Providers.AppClientScriptProvider.GetList", {oEnterpriseItem:AppName});
		oDataSet := DoProc("Enterprise_Server.DataSetSupport.DsFromString", {sDataSet});
		:FOR i := 0 :TO oDataSet:Tables[0]:Rows:Count - 1;
			oRow := oDataSet:Tables[0]:Rows[i];
			AAdd(aItems, {oRow["SCRIPTNAME"], sOutType, oRow["SCRIPTLANGUAGE"], sURI + "/" + oRow["SCRIPTNAME"], oRow["CHECKEDOUTBY"], NIL});
		:NEXT;
	:EXITCASE;

	:CASE oEnterpriseItem:Type == "APPSUBCATEGORY" .AND. oEnterpriseItem:Name == "DataSources";
		sOutType := "APPDS";
		bFolder := .F.;
		sDataSet := DoProc("Enterprise_Data_Providers.AppDataSourceProvider.GetList", {oEnterpriseItem:AppName});
		oDataSet := DoProc("Enterprise_Server.DataSetSupport.DsFromString", {sDataSet});
		:FOR i := 0 :TO oDataSet:Tables[0]:Rows:Count - 1;
			oRow := oDataSet:Tables[0]:Rows[i];
			AAdd(aItems, {oRow["DSNAME"], sOutType, oRow["SCRIPTLANGUAGE"], sURI + "/" + oRow["DSNAME"], oRow["CHECKEDOUTBY"], NIL});
		:NEXT;
	:EXITCASE;
	
	:CASE oEnterpriseItem:Type == "APPSUBCATEGORY" .AND. oEnterpriseItem:Name == "HTMLForms";
		bFolder := .F.;
		sDataSet := DoProc("Enterprise_Data_Providers.FormProvider.GetList", {oEnterpriseItem:AppName, "HTML"});
		oDataSet := DoProc("Enterprise_Server.DataSetSupport.DsFromString", {sDataSet});
		:DECLARE sFormName;
		:FOR i := 0 :TO oDataSet:Tables[0]:Rows:Count - 1;
			oRow := oDataSet:Tables[0]:Rows[i];
			sFormName := oEnterpriseItem:AppName + "." + oRow["FORMNAME"];
			sGUID := oRow["FORMID"];
			AAdd(aItems, {oRow["FORMNAME"] + " [XML]", "HTMLFORMXML", "XML", sURI + "/XML/" + oRow["FORMNAME"], oRow["CHECKEDOUTBY"], sGUID});
			AAdd(aItems, {oRow["FORMNAME"] + " [Code Behind]", "HTMLFORMCODE", "JS", sURI + "/CodeBehind/" + oRow["FORMNAME"], oRow["CHECKEDOUTBY"], sGUID});
			AAdd(aItems, {oRow["FORMNAME"] + " [Guide]", "HTMLFORMGUIDE", "JSON", sURI + "/Guide/" + oRow["FORMNAME"], oRow["CHECKEDOUTBY"], sGUID});
		:NEXT;
	:EXITCASE;
	
	:CASE oEnterpriseItem:Type == "APPSUBCATEGORY" .AND. oEnterpriseItem:Name == "XFDForms";
		bFolder := .F.;
		sDataSet := DoProc("Enterprise_Data_Providers.FormProvider.GetList", {oEnterpriseItem:AppName, "XFD"});
		oDataSet := DoProc("Enterprise_Server.DataSetSupport.DsFromString", {sDataSet});
		:FOR i := 0 :TO oDataSet:Tables[0]:Rows:Count - 1;
			oRow := oDataSet:Tables[0]:Rows[i];
			AAdd(aItems, {oRow["FORMNAME"] + " [XML]", "XFDFORMXML", "XML", sURI + "/XML/" + oRow["FORMNAME"], oRow["CHECKEDOUTBY"], NIL});
			AAdd(aItems, {oRow["FORMNAME"] + " [Code Behind]", "XFDFORMCODE", "JS", sURI + "/CodeBehind/" + oRow["FORMNAME"], oRow["CHECKEDOUTBY"], NIL});
		:NEXT;
	:EXITCASE;
	
	:CASE oEnterpriseItem:Type == "APPSUBCATEGORY" .AND. oEnterpriseItem:Name == "ServerScripts";
		sOutType := "APPSS";
		bFolder := .F.;
		sDataSet := DoProc("Enterprise_Data_Providers.AppServerScriptProvider.GetList", {oEnterpriseItem:AppName});
		oDataSet := DoProc("Enterprise_Server.DataSetSupport.DsFromString", {sDataSet});
		:FOR i := 0 :TO oDataSet:Tables[0]:Rows:Count - 1;
			oRow := oDataSet:Tables[0]:Rows[i];
			AAdd(aItems, {oRow["SCRIPTNAME"], sOutType, oRow["SCRIPTLANGUAGE"], sURI + "/" + oRow["SCRIPTNAME"], oRow["CHECKEDOUTBY"], NIL});
		:NEXT;
	:EXITCASE;
	
	:CASE oEnterpriseItem:Type == "SSCATEGORY";
		sOutType := "SS";
		bFolder := .F.;
		sDataSet := DoProc("Enterprise_Data_Providers.ServerScriptProvider.GetList", {oEnterpriseItem:Name});
		oDataSet := DoProc("Enterprise_Server.DataSetSupport.DsFromString", {sDataSet});
		:FOR i := 0 :TO oDataSet:Tables[0]:Rows:Count - 1;
			oRow := oDataSet:Tables[0]:Rows[i];
			AAdd(aItems, {oRow["SCRIPTNAME"], sOutType, oRow["SCRIPTLANGUAGE"], sURI + "/" + oRow["SCRIPTNAME"], oRow["CHECKEDOUTBY"], NIL});
		:NEXT;
	:EXITCASE;

	:CASE oEnterpriseItem:Type == "CSCATEGORY";
		sOutType := "CS";
		bFolder := .F.;
		sDataSet := DoProc("Enterprise_Data_Providers.ClientScriptProvider.GetList", {oEnterpriseItem:Name});
		oDataSet := DoProc("Enterprise_Server.DataSetSupport.DsFromString", {sDataSet});
		:FOR i := 0 :TO oDataSet:Tables[0]:Rows:Count - 1;
			oRow := oDataSet:Tables[0]:Rows[i];
			AAdd(aItems, {oRow["SCRIPTNAME"], sOutType, oRow["SCRIPTLANGUAGE"], sURI + "/" + oRow["SCRIPTNAME"], oRow["CHECKEDOUTBY"], NIL});
		:NEXT;
	:EXITCASE;
	
	:CASE oEnterpriseItem:Type == "DSCATEGORY";
		sOutType := "DS";
		bFolder := .F.;
		sDataSet := DoProc("Enterprise_Data_Providers.DataSourceProvider.GetList", {oEnterpriseItem:Name});
		oDataSet := DoProc("Enterprise_Server.DataSetSupport.DsFromString", {sDataSet});
		:FOR i := 0 :TO oDataSet:Tables[0]:Rows:Count - 1;
			oRow := oDataSet:Tables[0]:Rows[i];
			AAdd(aItems, {oRow["DSNAME"], sOutType, oRow["SCRIPTLANGUAGE"], sURI + "/" + oRow["DSNAME"], oRow["CHECKEDOUTBY"], NIL});
		:NEXT;
	:EXITCASE;
	
	:CASE oEnterpriseItem:Type == "ENT_CAT_LOG";
		sOutType := "SERVERLOG";
		bFolder := .F.;
		:DECLARE aDataSet;
		aDataSet := DoProc("Enterprise_Data_Providers.EnterpriseUtilsProvider.GetUsersWithLog");
		:FOR i := 1 :TO Len(aDataSet);
			AAdd(aItems, {aDataSet[i], sOutType, sOutType, sURI + "/" + aDataSet[i], "", NIL});
		:NEXT;
	:EXITCASE; 
	
	:CASE AScanExact({"APPSS", "APPCS", "APPDS", "HTMLFORMXML", "HTMLFORMGUIDE", 
			"HTMLFORMCODE", "XFDFORMXML", "XFDFORMCODE", "CS", "DS", "SS", "SERVERLOG"}, 
			oEnterpriseItem:Type) > 0;
		sOutType := oEnterpriseItem:Type;
		bFolder := .F.;

		:IF !Empty(oEnterpriseItem:AppName);
			sItemName := oEnterpriseItem:AppName + "." + oEnterpriseItem:Name;
		:ELSE;
			sItemName := oEnterpriseItem:CategoryName + "." + oEnterpriseItem:Name;
		:ENDIF;
		
		sGUID := oUtils:GetItemGUID(sItemName, oEnterpriseItem:Type);
		
		/* get checked out by user;
		:DECLARE sSQL, aReturn;
		sSQL := "select CHECKEDOUTBY from LIMSSOURCECONTROL where ITEMID = ? and DONE = 0";
		aReturn := LSelect(sSQL,, "DICTIONARY", {sGUID});
		:IF Empty(aReturn);
			sCheckedOutBy := "";
		:ELSE;
			sCheckedOutBy := aReturn[1][1];
		:ENDIF;
		AAdd(aItems, {oEnterpriseItem:Type, sOutType, sOutType, sURI, sCheckedOutBy, sGUID});
	:EXITCASE; 
	
	:CASE oEnterpriseItem:Type == "ENT_CAT_TABLES";
		aItems := aTablesSubCategories;
		sOutType := "CATEGORY";
		bFolder := .T.;
	:EXITCASE;

	:CASE oEnterpriseItem:Type == "TBLCATEGORY";
		sOutType := "TABLE";
		sDataSet := DoProc("Enterprise_Data_Providers.TableProvider.GetList", {Upper(oEnterpriseItem:Name)});
		oDataSet := DoProc("Enterprise_Server.DataSetSupport.DsFromString", {sDataSet});
		:FOR i := 0 :TO oDataSet:Tables[0]:Rows:Count - 1;
			oRow := oDataSet:Tables[0]:Rows[i];
			AAdd(aItems, {oRow["TABLENAME"], sOutType, sOutType, sURI + "/" + oRow["TABLENAME"], oRow["CHECKEDOUTBY"], oRow["TABLEID"]});
		:NEXT;		
		bFolder := .F.;
	:EXITCASE;

:ENDCASE;

:DECLARE aResultItems;
aResultItems := {};
:FOR i := 1 :TO Len(aItems);
	:DECLARE sLanguage;
	sLanguage := aItems[i][3];
	:BEGINCASE;
		:CASE Upper(sLanguage) == "STARLIMS";
			sLanguage := "SSL";
		:EXITCASE;
		:CASE Upper(sLanguage) == "JSCRIPT";
			sLanguage := "JS";
		:EXITCASE;
		:CASE Upper(sLanguage) == "SERVERLOG";
			sLanguage := "SERVERLOG";
		:EXITCASE;
	:ENDCASE;

	:DECLARE oItem;
	oItem := oUtils:RemoveDBNull(CreateUDObject({
		{"name", aItems[i][1]},
		{"type", aItems[i][2]},
		{"uri", aItems[i][4]},
		{"checkedOutBy", aItems[i][5]},
		{"language", sLanguage},
		{"isFolder", bFolder},
		{"guid", aItems[i][6]}
	}));
	AAdd(aResultItems, oItem);
:NEXT;

oResult := CreateUDObject({
	{"success", .T.},
	{"data", CreateUDObject({{"items", aResultItems}})}
});

Response:StatusCode := 200;
:RETURN oResult;

:PROCEDURE _GetCategoryItems;
:PARAMETERS sDataSet, sOutType, sURI;
:DEFAULT sDataSet, "";
:DEFAULT sOutType, "";
:DEFAULT sURI, "";

:DECLARE aItems, i, oDataSet, oRow;
	aItems := {};
	:IF !Empty(sDataSet);
		oDataSet := DoProc("Enterprise_Server.DataSetSupport.DsFromString", {sDataSet});
		:FOR i := 0 :TO oDataSet:Tables[0]:Rows:Count - 1;
			oRow := oDataSet:Tables[0]:Rows[i];
			AAdd(aItems, {oRow["CATNAME"], sOutType, "N/A", sURI + "/" + oRow["CATNAME"], NIL, NIL});
		:NEXT;
	:ENDIF;

	:RETURN aItems;
:ENDPROC;